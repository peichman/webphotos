#!/usr/bin/perl -w
use strict;

use Template;
use Image::MetaData::JPEG;
use Getopt::Long;

GetOptions(
    'metadata=s' => \my $METADATA_FILE,
    'index-only|x' => \my $INDEX_ONLY,
);

# default metadata filename
$METADATA_FILE ||= 'dc.yml';

my @files = @ARGV;

my $template = Template->new({
    INCLUDE_PATH => [qw{/home/peter/projects/webphotos}],
    ENCODING => 'utf8',
});
use YAML;

my $gallery_metadata = -e $METADATA_FILE ? YAML::LoadFile($METADATA_FILE) : {};

if (!$INDEX_ONLY) {
    for my $i (0 .. @files - 1) {
        my $prev = $i > 0 ? $files[$i - 1] : '';
        my $next = $i < @files - 1 ? $files[$i + 1] : '';
        my $this = $files[$i];
        #my $image = Image::MetaData::JPEG->new($this, 'COM', 'FASTREADONLY');
        #my @segments = $image->get_segments('COM');
        my $comment;
        # check for a .txt comment sidecar
        (my $txt_file = $this) =~ s/\.jpg/.txt/i;
        if (-e $txt_file) {
            open my $fh, '<', $txt_file;
            $comment = join '', <$fh>;
        } elsif (($gallery_metadata->{$this} || {})->{description}) {
            $comment = $gallery_metadata->{$this}{description};
        } else {
            $comment = `exiv2 -pc $this`;
        }

        my $outfile = sprintf '%03d.html', $i + 1;
        print "$outfile\n";
        $template->process(
            'photo.html',
            {
                prev => $prev,
                next => $next,
                file => $this,
                comment => $comment,
                index => $i + 1,
                count => scalar @files,
                gallery => $gallery_metadata,
            },
            $outfile,
            { binmode => ':encoding(UTF-8)' },
        ) or die $template->error;
        printf "%15s < %s > %-15s\n", $prev, $this, $next;
    }
}

$template->process(
    'index.tt',
    {
        files => \@files,
        gallery => $gallery_metadata,
    },
    'index.html',
    { binmode => ':encoding(UTF-8)' },
) or die $template->error;
